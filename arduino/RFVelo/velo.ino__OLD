#include "printf.h"
#include "RFCore.h"
#include <Servo.h>
#include <avr/sleep.h>
#include <avr/power.h>
#include "Rfid.h"
#include "Adafruit_NeoPixel.h"

/************************/
/** PINs configuration **/
/************************/
/* RF
black module config: http://www.seeedstudio.com/document/pics/Interface.jpg
1 - GND       |8 7 |
2 - VCC       |6 5 |
3 - CE        |4 3 |
4 - CSN       |2[1]|
5 - SCK
6 - MOSI
7 - MISO
8 - IRQ */

/* pour RF24 radio(9,10) as in RFCore (http://tmrh20.github.io/RF24/ (avec 7 -> 9 et 8 -> 10))
GND -> GND
VCC -> 3V3
CE  -> 9
CSN ->10
MOSI ->11
MISO ->12
SCK ->13
IRQ -> 2
*/
/* Buttons */
const int BUTTON_PIN1 = 14; //A0
const int BUTTON_PIN2 = 15; //A1
/* Locker */
const int MOTOR_PIN1 = A4; //A4
const int MOTOR_PIN2 = A3; //A3
const int MOTOR_ENABLE = 5; //A2
const int BUTTON_MOTOR_STOP_OPEN = 3;
const int BUTTON_MOTOR_STOP_CLOSE = 4;

/* RFID */
const int RFID_ACTIVATE = 3; // OUTPUT 1 TO ACTIVATE THE LED

const int RFID_IN = 8; //final version will use pin 0 (serial)
const int RFID_OUT = 8;//final version will use pin 1 (without serial.begin, it makes them unusable)

/* LED */
const int LED_PIN = 6; // NeoPixel

/************************/
/** ARBITRARY CONSTANTS**/
/************************/
/*LED COLOURS*/
const int COLOUR_OFF=0;
const int COLOUR_RED=1;
const int COLOUR_YELLOW=2;
const int COLOUR_ORANGE=3;
const int COLOUR_GREEN=4;
const int COLOUR_BLUE=5;
const int COLOUR_WHITE=6;
//MOTOR

const int RFID_TIMEOUT = 10000; //10 sec to put the rfid card
const int DEBOUNCE_DELAY =200;
const int USER_CODE_TIMEOUT = 10000; //10 sec to type the code
const int USER_CODE_LENGTH = 4;
const int MAX_ATTEMPT_TO_SEND_DATA = 3; // number attempts to send data to the born/terminal
const int DELAY_ATTEMPT_TO_SEND_DATA = 300; // 0.3 sec to wait between 2 attemps of sending data to the born/terminal
const int MOTOR_MOVEMENT_DELAY = 500;
const int MOTOR_MOVEMENT_TIMEOUT = 250;

const int MOTOR_SPEED = 200; //100
/***********************************/


bool terminal_in_range = false;

Rfid rfid(RFID_IN,RFID_OUT);
RFCore * rf_core;

byte client_rfid[6] = {34,35,36,37,38,39};

unsigned char user_code[6]= {0,0,0,0,0,0}; //All char array about to be send must be the size of one packet

byte user_code_byte = 0x0;

Adafruit_NeoPixel pixels = Adafruit_NeoPixel(1, LED_PIN, NEO_KHZ800);

void setup(void) {
	Serial.begin(115200);

	MCUCR = (1<<ISC01) | (1<<ISC00); //01 and 00 for triggering interrupts on change at low level

	printf_begin();

	pinMode(BUTTON_PIN1, INPUT);
	pinMode(BUTTON_PIN2, INPUT);

	pinMode(RFID_ACTIVATE, OUTPUT);

	pinMode(MOTOR_PIN1, OUTPUT);
	pinMode(MOTOR_PIN2, OUTPUT);
	pinMode(MOTOR_ENABLE, OUTPUT);

	int BIKE_ID = 8;//will read EEPROM after
	rf_core = new RFCore(BIKE_ID, false);
	pixels.begin();
	interrupts();
	switchButtonsInterrupts(false);
	disableRFID();
}

void loop() {

	//sleepNow();
	printf("AWAKE!\n\r");

	terminal_in_range = rf_core->rangeTest();
	//rf_core->debug();

	if(terminal_in_range) {
		printf("Terminal in range!\n\r");
		switchLed(COLOUR_WHITE);
		enableRFID();
		delay(500);
		bool rfid_user_received=getRFID();
		disableRFID();
		if (!rfid_user_received) {
			printf("No RFID given\n\r");
			switchLed(COLOUR_RED);
		} else {
			printf("Get RFID : ");
			for(int i =0; i<6;i++){
				Serial.print(client_rfid[i],HEX);
			}
			printf("\n\r");
			switchLed(COLOUR_BLUE);

			if(!getUserCode()) {
				printf("No user code given\n\r");
			} else {
				printf("User code given : ");

				for(int i =0; i<USER_CODE_LENGTH;i++){
					Serial.print(user_code[i]);
				}
				printf("\n\r");
				switchLed(COLOUR_YELLOW);

				printf("%i\n\r", user_code_byte);

				if(canWithdraw()) {
					printf("Ok withdraw\n\r");
					switchLed(COLOUR_GREEN);
					openLock();
					delay(3000);
					closeLock();
					delay(500);
				} else {
					printf("No withdraw\n\r");
					switchLed(COLOUR_RED);
					delay(3000);
				}
			}
		}
	}

	switchLed(COLOUR_OFF);
	printf("Go to sleep\n\r");
	sleepNow();
}

void switchLed(int color) {
	int r,g,b;
	r=g=b=0;
	switch(color){
		case COLOUR_RED:
		r = 255;
		break;
		case COLOUR_YELLOW:
		r = 255;
		g = 255;
		break;
		case COLOUR_ORANGE:
		r = 255;
		g = 128;
		break;
		case COLOUR_GREEN:
		g=255;
		break;
		case COLOUR_BLUE:
		b=255;
		break;
		case COLOUR_WHITE:
		r=g=b=255;
		break;

		default:
		case COLOUR_OFF:
		r=g=b= 0;
	}
	//printf("LED COLOR %d - R: %d G: %d B: %d \n\r",color,r,g,b);
	pixels.setPixelColor(0, pixels.Color(r,g,b));
	pixels.show();
}

/**
* Try to read the RFID Data. There is a timeout given by RFID_TIMEOUT
*
* Return true if RFID code whare read (the data is stored in client_rfid)
* return false otherwise
*/
boolean getRFID() {
	int actual_time= millis();
	int initial_time = actual_time;
	boolean received = false;
	while((actual_time-initial_time) < RFID_TIMEOUT && !received) {
		received = rfid.RFIDRead(&client_rfid[0]);
		actual_time = millis();
		delay(50);
	}
	return received;
}


void enableRFID() {
	digitalWrite(RFID_ACTIVATE, HIGH);
}

void disableRFID() {
	digitalWrite(RFID_ACTIVATE, LOW);
}

/********************/
/* LOCKER FUNCTIONS  */
/********************/

void actionLock(bool open) {
	int actual_time= millis();
	int initial_time = actual_time;
	bool button_pressed = false;

	if(open) {
		Serial.print("openLock - \n\r");
	} else {
		Serial.print("closeLock - \n\r");
	}

	analogWrite(MOTOR_ENABLE, MOTOR_SPEED);
	//digitalWrite(MOTOR_ENABLE,HIGH);
	//Serial.print("%i - \n\r", MOTOR_SPEED);
	delay(20);

	if(open) {
		digitalWrite(MOTOR_PIN1, HIGH); // set pin 2 on L293D low
		digitalWrite(MOTOR_PIN2, LOW);
	} else {
		digitalWrite(MOTOR_PIN1, LOW); // set pin 2 on L293D low
		digitalWrite(MOTOR_PIN2, HIGH);
	}

	while((actual_time-initial_time) < MOTOR_MOVEMENT_TIMEOUT && ! button_pressed) {
		actual_time= millis();

		int button_open = digitalRead(BUTTON_MOTOR_STOP_OPEN);
		int button_close = digitalRead(BUTTON_MOTOR_STOP_CLOSE);

		if(button_open== HIGH && open) {
			Serial.print("open pressed - \n\r");
			button_pressed = true;
		}

		if(button_close == HIGH && (!open)) {
			Serial.print("close pressed - \n\r");
			button_pressed = true;
		}
	}

	Serial.print("fin \n\r");

	analogWrite(MOTOR_ENABLE, LOW);
}


void openLock() {
	actionLock(true);
}

void closeLock() {
	actionLock(false);
}

/**
* Try to get the User Code.
* This function is customized by the following constants :
* - USER_CODE_TIMEOUT : timeout (in milliseconds)
* - USER_CODE_LENGTH : the lenth of the code
*
* Return true if the code is read and store the variable in the variable user_code
* Return false otherwise
*/
boolean getUserCode(){
	int button_state1=LOW,button_state2=LOW,index_key=0;
	int actual_time= millis();
	int initial_time = actual_time;
	boolean buttons_released =false;
	user_code_byte = 0x0;

	while((actual_time-initial_time) < USER_CODE_TIMEOUT && index_key < USER_CODE_LENGTH){
		actual_time = millis();
		button_state1 = digitalRead(BUTTON_PIN1);
		button_state2 = digitalRead(BUTTON_PIN2);

		if(button_state1== LOW && button_state2 == LOW){ // if both buttons are released
			buttons_released = true;
			digitalWrite(BUTTON_PIN1,LOW);
		}
		//check if user has released the buttons and if only one is pressed
		else if(buttons_released && (button_state1== HIGH ^ button_state2 == HIGH))
		{
			// verify which button is pressed and if not both are pressed
			if (button_state1 == HIGH && button_state2 == LOW) {
				user_code[index_key] = 0;
				index_key++;

				user_code_byte = user_code_byte | 0X0;
				if(index_key != USER_CODE_LENGTH) {
					user_code_byte = user_code_byte << 1;
				}
				switchLed(COLOUR_BLUE);
			}
			else if(button_state2 == HIGH && button_state1 == LOW){
				switchLed(COLOUR_OFF);
				user_code[index_key] = 1;
				index_key++;

				user_code_byte = user_code_byte | 0X1;
				if(index_key != USER_CODE_LENGTH) {
					user_code_byte = user_code_byte << 1;
				}


				switchLed(COLOUR_BLUE);
			}
			buttons_released = false;
			delay(DEBOUNCE_DELAY); //to avoid contact bounce
		}
	}
	return (index_key == USER_CODE_LENGTH);
}

boolean canWithdraw() {
	if(rf_core->handShake()) {
		printf("Success handshake!\n\r");

		uint8_t user_datas[8];
		for(int i =0; i<6;i++){
			user_datas[i]=client_rfid[i];
		}
		user_datas[6]= user_code_byte;
		user_datas[7]= 0;
		//user_datas[7]= '\O';

		while(true){
			rf_core->sendPacket(user_datas);
			delay(200);
		}

		//int response = rf_core->withdrawPermission(user_code_byte, &client_rfid);
		int response = 0;
		printf("response : %i\n\r", response);

		rf_core->closeSession();

		return response == 1;
	}
	return false;
}


/********************/
/* SLEEP FUNCTIONS  */
/********************/

ISR(PCINT1_vect) { //WARNING: comment the ISR(PCINT1_vect) definition in SofwareSerial.cpp
	switchButtonsInterrupts(false); //disable interrupt on buttons, arduino is awake, that avoid the interrupt to fire after wake.
}
/**
* Set the bike in sleep mod. The bike is awaked when a button is pressed.
*/

void sleepNow() {
	//http://donalmorrissey.blogspot.com.es/2010/04/putting-arduino-diecimila-to-sleep.html
	switchButtonsInterrupts(true); //activate interupt on button
	delay(100);
	rf_core->powerDownRadio();
	// Choose our preferred sleep mode:
	set_sleep_mode(SLEEP_MODE_PWR_DOWN);
	//
	//interrupts();
	//
	// Set sleep enable (SE) bit:
	sleep_enable();
	//

	sleep_mode();
	//
	// Upon waking up, sketch continues from this point.
	rf_core->powerUpRadio();
	sleep_disable();
}
void switchButtonsInterrupts(boolean on) { //TRUE for ON, FALSE for off
	/* Pin to interrupt map:
	* D0-D7 = PCINT 16-23 = PCIE2 = pcmsk2
	* D8-D13 = PCINT 0-5 = PCIE0 = pcmsk0
	* A0-A5 (D14-D19) = PCINT 8-13 = PCIE1 = pcmsk1*/
	//interrupt on analog PIN A0 y A1
	if(on) { //enable interrupts
		PCICR |= (1<<PCIE1);
		PCMSK1 |= (1<<PCINT8); //8 for A0 pin change mask get loaded a pin change interrupt on A0.
		PCMSK1 |= (1<<PCINT9); //9 for A1
	}
	else {//disable interrupts
		PCICR &= ~(1<<PCIE1);
		PCMSK1 &= ~(1<<PCINT8); //8 for A0 pin change mask get loaded a pin change interrupt on A0.
		PCMSK1 &= ~(1<<PCINT9); //9 for A1
	}
}
